buildscript {
    repositories {
        //maven {
        //  url 'https://oss.sonatype.org/content/repositories/snapshots/'
        //}
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.3') {
            // Workaround for https://github.com/stanfy/spoon-gradle-plugin/issues/33
            exclude module: 'guava'
        }
        classpath 'com.google.guava:guava:18.0'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the lombok version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'spoon'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.sknutti.capstoneproject"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        internal {
            applicationId 'com.sknutti.capstoneproject.internal'
        }
        production {
            applicationId 'com.sknutti.capstoneproject'
        }
    }

    signingConfigs {
        production
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
//        fatal 'UnusedResources'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

configurations {
    internalDebugCompile
}

android.signingConfigs.whenObjectAdded { signing ->
    setupSigning(signing.name)
}

android.productFlavors.whenObjectAdded { flavor ->
    flavor.signingConfig = android.signingConfigs[flavor.name]
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    internalCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    productionCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    internalCompile 'com.mattprecious.telescope:telescope:1.5.0@aar'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:support-annotations:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.2'
    compile 'com.roomorama:caldroid:2.2.1'
}
